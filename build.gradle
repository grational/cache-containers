/*
 * This build file was auto generated by 'gigawatt'
 * @author gsus
 * @date 2021-10-10 17.05
 */

// [plugins] this block must be the first
plugins {
	// support for groovy
	id 'groovy'
	// required to use the 'api' function in the dependencies section
	id 'java-library'
	// required by jitpack
	id 'maven-publish'
	// awesome git tag / gradle version integration
	id 'pl.allegro.tech.build.axion-release' version '1.13.14'
}

// [general] configuration
project.ext {
	groovyVersion = '3.0.21'
}

scmVersion {
	tag {
		prefix = 'release-'
	}
}

project.with {
	//project.name is set in the settings.gradle with rootProject.name
	group       'it.grational'
	version     scmVersion.version // '0.5.0' (remove -SNAPSHOT for releases)
	description 'Allow to create a transparent permanent compressed cache stored externally.'
}

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
}

dependencies {
	implementation (
		"org.codehaus.groovy:groovy:$groovyVersion",
		"redis.clients:jedis:5.1.2"
	)

	api (
		"com.github.grational:hash-functions:release-1.1.1",
		"com.github.grational:compressors:release-0.1.3"
	)

	testImplementation (
		"org.spockframework:spock-core:2.3-groovy-3.0",
		"org.codehaus.groovy:groovy-dateutil:$groovyVersion"
	)
}

// [tasks] configuration //
tasks.withType(Test) {
	useJUnitPlatform()
	maxParallelForks = Runtime.runtime.availableProcessors() / 2
}

// required to work with jitpack.io
publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = 'com.github.grational'
			artifactId = 'cache-containers'
			version = "release-${scmVersion.version}" // '0.5.0' (remove -SNAPSHOT for releases)

			from components.java
		}
	}
}

// [tasks] configuration //
jar {
	// execute check task when create the jar
	dependsOn 'check'
}

check {
	// clean before check
	dependsOn 'clean'
}
// vim: ft=groovy:fdm=indent
